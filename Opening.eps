import Skill as sq;
import Display as tx;
import System as sy;
import Shop as so;
import Enemy as ey;
import Player as pr;

/* 변수 모음 */
var STimer = 0; //오프닝 변수
var VictoryCondition = 0; //보스 처치시 1로변함 선택 끝나면 0으로
var Luck = rand();

const Key_Input = PVariable(); //키인식
const Key_Ws = PVariable(); //방향키 인식용1
const Key_Ad = PVariable(); //방향키 인식용2

const Hero_Choose_Select = $L("Choose_1");
const Hero_Choose_Main = $L("Choose_Cha");
const Hero_Choose_Nearby = $L("Explain_1");
const BattleGround = $L("MainStage");
const Chooser = 45;

var Mode = 0; //게임 난이도

const RerollCost = PVariable();         //상점 리롤 가격
const RerollTry = PVariable();          //리롤한횟수
const IceShop = PVariable();            //상점얼리기 0or1

const GameoverTimer = PVariable(); //게임종료 타이머. 전체 플레이어가 사망할 경우 10초뒤 패배
const ReviveTimer = PVariable(); //부활타이머. 5초 동안 유지되면 부활

var Speed_Option = 0;   //배속옵션 기본배속 0 1배속 1 2배속 2

const ItemChoice = PVariable(); //아이템 상점 선택한 아이템

/* 온플긴 실행 함수 */
function KeyInit() {
    EUDRegisterObjectToNamespace("Key", Key_Input);
    EUDRegisterObjectToNamespace("Ws", Key_Ws);
    EUDRegisterObjectToNamespace("Ad", Key_Ad);
}
/*
0~24: 시작대기
25~28: 난이도설정
29~80: 난이도표시
81: 랜덤스킬 설정
82: 스테이지 시작
83: 상점페이즈
 */

//시작부분
function Opening() {
    const Concp = sy.Controller;
    if (STimer < 21) {STimer += 1;} //대기

    if (STimer >= 21 && STimer <= 24) { 
        //게임 난이도 선택
        if (Key_Input[Concp] == 1) { //쉬움
            STimer = 22;
            Mode = 1;
        }
        else if (Key_Input[Concp] == 2) { //보통
            STimer = 23;
            Mode = 2;
        }
        else if (Key_Input[Concp] == 3) { //어려움
            STimer = 24;
            Mode = 3;
        } 
        else if (Key_Input[Concp] == 6 && Mode >= 1) { //선택완료
            STimer = 25;
            for (var i = 0; i < 3; i++) {
                if (playerexist(i)) SetResources(i, SetTo, 1250-Mode*250, Ore);
                if (playerexist(i)) SetResources(i, Add, 500000, Ore);
            }
        }
    }
    else if (STimer == 25) { //레벨 설명
        STimer += 1;
    }
    else if (STimer >= 26 && STimer <= 75) { //대기
        STimer += 1;
    }
    else if (STimer == 76) { //캐릭터 선택 셋팅
        //영웅 유닛 생성
        for (var i = 0; i < 6; i++) {
            const epd = sy.SetNextUnitEPD();
            sy.HeroCreation(Hero_Choose_Select+i, i);
            sy.SetExtraDb(epd, 0, 5);
            SetMemoryXEPD(epd + 0xDC/4, SetTo, 0x200000, 0x200000);
            SetMemoryXEPD(epd + 0xDC/4, SetTo, 0x800000, 0x800000);
        }
        //선택 유닛 생성
        for (var i = 0; i < 3; i++) {
            if (playerexist(i)) CreateUnit(1, Chooser, Hero_Choose_Main, i);
        }
        STimer += 1;
    }
    else if (STimer == 77) { //영웅 선택
        if (Command(Force1, Exactly, 0, Chooser)) STimer += 1;
    }
    else if (STimer >= 78 && STimer <= 79) {STimer += 1;}
    else if (STimer == 80) {
        STimer += 1;
    }
    else if (STimer == 81) { //기본설정
        STimer += 1;
        MoveUnit(All, "Men", Force1, Hero_Choose_Main, BattleGround);
        RemoveUnit("Men", P5);
    }
    else if (STimer == 82) { //게임시작
        ey.EnemySummon(Mode);
        ey.EnemyGroup();
    }
    foreach(cp : EUDLoopPlayer("Human")) {
        setcurpl(cp);
        if (STimer >= 21 && STimer <= 24) {
            tx.Display_LevelSetting(Mode);
        }
        else if (STimer == 25) { //레벨 설명
            tx.Display_Reset();
            tx.Display_LevelExplain(Mode);
        }
        else if (STimer == 76) { //캐릭터 선택 셋팅
            tx.Display_Reset();
            CenterView(Hero_Choose_Main);
        }
        else if (STimer == 77) {
            for (var i = 0; i < 8; i++) {
                if (Bring(cp, AtLeast, 1, Chooser, Hero_Choose_Nearby+i)) {
                    //영웅 설명 보여주기
                    tx.Display_HeroInfo(i);
                }
                if (Bring(cp, AtLeast, 1, Chooser, Hero_Choose_Select+i)) {
                    //영웅 선택 완료
                    KillUnitAt(1, Chooser, Hero_Choose_Select+i, cp);
                    GiveUnits(1, i, P5, Hero_Choose_Select+i, cp);
                    sy.SetStatInit(cp, i);
                    ey.IsHeroAlive[cp] = 1;
                }
            }
        }
        else if (STimer == 78) {
            pr.GameSetting(cp);
        }
        else if (STimer == 80) {
            tx.Display_Reset();
        }
        else if (STimer == 82) { //게임 시작
            //상시발동
            pr.GamePlay(cp, Key_Input[cp], Key_Ad[cp], Key_Ws[cp]);
        }
    }
}
function Checkdefeat(cp) {
}